group 'de.entrecode'
version '0.4.0'

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = "ec-datamanager-java-sdk"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'com.squareup.okhttp:mockwebserver:2.4.0'
    testCompile 'com.jayway.awaitility:awaitility:1.6.3'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.google.code.gson:gson:2.3.1'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

test {
    // we always want the tests to be executed
    outputs.upToDateWhen { false }
    /*
    testLogging {
        events 'started', 'passed'
    }
    */
}

jacoco {
    toolVersion = "0.7.1.201405082137"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}
jacocoTestReport.dependsOn test

task clover(type: Exec) {
    workingDir "$buildDir/reports/jacoco/test"

    commandLine '../../../../cover2cover/run.sh'
}
clover.dependsOn jacocoTestReport

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            //repository(url: "file://localhost/Users/simon/Documents/entrecodeGmbH/MavenRepo/")

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Java SDK for ec.datamanager'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'The official Java SDK for Datamanager by entrecode. By entrecode.'
                url 'https://entrecode.de'

                scm {
                    connection 'scm:git:https://github.com/entrecode/ec.datamanager-java-sdk.git'
                    developerConnection 'scm:git:https://github.com/entrecode/ec.datamanager-java-sdk.git'
                    url 'git@github.com:entrecode/ec.datamanager-java-sdk.git'
                }
                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'scherzinger'
                        name 'Simon Scherzinger'
                        email 'scherzinger@entrecode.de'
                    }
                }
            }
        }
    }
}